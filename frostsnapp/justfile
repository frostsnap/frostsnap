import '../fetch.just'

default: gen run

API_DIR := "rust/src/api"
CANARY  := "binding-rerun.sha256"

gen +ARGS="":
    flutter_rust_bridge_codegen generate {{ARGS}}
    find {{API_DIR}} -type f -exec sha256sum {} + > {{CANARY}}

build-runner:
    dart run build_runner build --delete-conflicting-outputs

build TARGET="linux" +ARGS="": maybe-gen
    #!/bin/sh
    BUILD_COMMIT=$(just get-build-commit)
    BUILD_VERSION=$(just get-build-version)
    BUNDLE_FIRMWARE=1 flutter build {{TARGET}} --dart-define=BUILD_COMMIT="$BUILD_COMMIT" --dart-define=BUILD_VERSION="$BUILD_VERSION" {{ARGS}}

run +ARGS="": maybe-gen
    #!/bin/sh
    set -e
    just ../gen-firmware
    BUILD_COMMIT=$(just get-build-commit)
    BUILD_VERSION=$(just get-build-version)
    BUNDLE_FIRMWARE=1 flutter run --flavor direct --dart-define=BUILD_COMMIT="$BUILD_COMMIT" --dart-define=BUILD_VERSION="$BUILD_VERSION" {{ARGS}}

run-secure +ARGS="": maybe-gen
    #!/bin/sh
    just ../build-secure
    BUILD_COMMIT=$(just get-build-commit)
    BUILD_VERSION=$(just get-build-version)
    BUNDLE_FIRMWARE=1 flutter run --flavor direct --dart-define=BUILD_COMMIT="$BUILD_COMMIT" --dart-define=BUILD_VERSION="$BUILD_VERSION" {{ARGS}}

maybe-gen:
    #!/bin/sh
    if ! sha256sum --check {{CANARY}} >/dev/null 2>&1 ; then
       echo "{{CANARY}} changed so re-running bindgen">&2;
       just gen
    fi
    just build-runner

flutter_rust_bridge_version:
    #!/bin/sh
    flutter_rust_bridge_version=$(cargo tree --prefix none  | sed -n 's/flutter_rust_bridge v//p')
    echo $flutter_rust_bridge_version


install-cargo-bins:
    #!/bin/sh
    flutter_rust_bridge_version=$(just flutter_rust_bridge_version)
    if ! test "$flutter_rust_bridge_version"; then
        echo "couldn't determine version for flutter_rust_bridge" >&2;
        exit 1
    fi
    cargo install cargo-ndk@3.5.4 espflash@3.2.0 cargo-expand@1.0.95 "flutter_rust_bridge_codegen@$flutter_rust_bridge_version"

get-build-commit:
    #!/bin/sh
    BUILD_COMMIT=$(git rev-parse HEAD 2>/dev/null || echo "unknown")
    if [ "$BUILD_COMMIT" != "unknown" ] && ! git diff-index --quiet HEAD --; then
        BUILD_COMMIT="${BUILD_COMMIT}-modified"
    fi
    echo "$BUILD_COMMIT"

get-build-version:
    #!/bin/sh
    BUILD_VERSION=$(git describe --tags --always 2>/dev/null || echo "unknown")
    if [ "$BUILD_VERSION" != "unknown" ] && ! git diff-index --quiet HEAD --; then
        BUILD_VERSION="${BUILD_VERSION}-modified"
    fi
    echo "$BUILD_VERSION"

build-appimage +ARGS="":
    #!/bin/sh
    if [ ! -d "build/linux/x64/release/bundle" ]; then
        echo "ERROR: Flutter build not found at build/linux/x64/release/bundle" >&2
        echo "Run 'just build linux --release' first" >&2
        exit 1
    fi
    dir="appimage/AppDir"
    executable="$dir/usr/Frostsnap"
    rm -rf "$dir"
    mkdir -p "$dir/usr"
    cp -r appimage/assets/* "$dir/"
    cp -rp build/linux/x64/release/bundle/* $dir/usr
    # github actions caching loses the 'u+x'
    chmod u+x "$executable" || exit 1;

    echo "final bundled libs:" >&2;
    ls -l $dir/usr/* >&2
    just fetch https://github.com/linuxdeploy/linuxdeploy/releases/download/1-alpha-20250213-2/linuxdeploy-x86_64.AppImage appimage/linuxdeploy
    chmod u+x appimage/linuxdeploy

    export LD_LIBRARY_PATH="$dir/usr/lib:${LD_LIBRARY_PATH:-}"
    appimage/linuxdeploy \
    --appdir $dir \
    --desktop-file "$dir/com.frostsnapp.app.desktop" \
    --icon-file "$dir/Frostsnap.png" \
    --executable "$executable" \
    --output appimage

# Create DMG from an existing .app bundle
package-dmg APP_PATH="build/macos/Build/Products/Release/Frostsnap.app":
    #!/bin/sh
    set -e

    APP_PATH="{{APP_PATH}}"
    APP_NAME="Frostsnap"
    DMG_NAME="${APP_NAME}.dmg"

    if [ ! -d "$APP_PATH" ]; then
        echo "ERROR: App not found at $APP_PATH" >&2
        exit 1
    fi

    rm -f "$DMG_NAME"

    if ! command -v create-dmg &> /dev/null; then
        echo "ERROR: create-dmg is required but not installed" >&2
        echo "Install with: brew install create-dmg" >&2
        exit 1
    fi

    create-dmg \
      --volname "${APP_NAME}" \
      --volicon "macos/Runner/Assets.xcassets/AppIcon.appiconset/app_icon_1024.png" \
      --background "macos/dmg-background.png" \
      --window-pos 200 120 \
      --window-size 600 400 \
      --icon-size 100 \
      --icon "${APP_NAME}.app" 150 185 \
      --hide-extension "${APP_NAME}.app" \
      --app-drop-link 450 185 \
      --no-internet-enable \
      --hdiutil-quiet \
      "$DMG_NAME" \
      "$APP_PATH"

    echo "Created DMG: $DMG_NAME" >&2
    ls -lh "$DMG_NAME" >&2

clean:
    flutter clean
    cd rust && cargo clean

import '../fetch.just'

default: gen run

API_DIR := "rust/src/api"
CANARY  := "binding-rerun.sha256"

gen +ARGS="":
    flutter_rust_bridge_codegen generate {{ARGS}}
    find {{API_DIR}} -type f -exec sha256sum {} + > {{CANARY}}

build TARGET="linux" +ARGS="": maybe-gen
    #!/bin/sh
    BUILD_COMMIT=$(just get-build-commit)
    BUNDLE_FIRMWARE=1 flutter build {{TARGET}} --dart-define=BUILD_COMMIT="$BUILD_COMMIT" {{ARGS}}

run +ARGS="": maybe-gen
    #!/bin/sh
    just ../gen-firmware
    BUILD_COMMIT=$(just get-build-commit)
    BUNDLE_FIRMWARE=1 flutter run --dart-define=BUILD_COMMIT="$BUILD_COMMIT" {{ARGS}}

run-secure +ARGS="": maybe-gen
    #!/bin/sh
    just ../build-secure
    BUILD_COMMIT=$(just get-build-commit)
    BUNDLE_FIRMWARE=1 flutter run --dart-define=BUILD_COMMIT="$BUILD_COMMIT" {{ARGS}}

maybe-gen:
    #!/bin/sh
    if ! sha256sum --check {{CANARY}} >/dev/null 2>&1 ; then
       echo "{{CANARY}} changed so re-running bindgen">&2;
       just gen
    fi

flutter_rust_bridge_version:
    #!/bin/sh
    flutter_rust_bridge_version=$(cargo tree --prefix none  | sed -n 's/flutter_rust_bridge v//p')
    echo $flutter_rust_bridge_version


install-cargo-bins:
    #!/bin/sh
    flutter_rust_bridge_version=$(just flutter_rust_bridge_version)
    if ! test "$flutter_rust_bridge_version"; then
        echo "couldn't determine version for flutter_rust_bridge" >&2;
        exit 1
    fi
    cargo install cargo-ndk@3.5.4 espflash@3.2.0 cargo-expand@1.0.95 "flutter_rust_bridge_codegen@$flutter_rust_bridge_version"

get-build-commit:
    #!/bin/sh
    BUILD_COMMIT=$(git rev-parse HEAD 2>/dev/null || echo "unknown")
    if [ "$BUILD_COMMIT" != "unknown" ] && ! git diff-index --quiet HEAD --; then
        BUILD_COMMIT="${BUILD_COMMIT}-modified"
    fi
    echo "$BUILD_COMMIT"

build-appimage +ARGS="":
    #!/bin/sh
    if [ ! -d "build/linux/x64/release/bundle" ]; then
        echo "ERROR: Flutter build not found at build/linux/x64/release/bundle" >&2
        echo "Run 'just build linux --release' first" >&2
        exit 1
    fi
    dir="appimage/AppDir"
    executable="$dir/usr/Frostsnap"
    rm -rf "$dir"
    mkdir -p "$dir/usr"
    cp -r appimage/assets/* "$dir/"
    cp -rp build/linux/x64/release/bundle/* $dir/usr
    # github actions caching loses the 'u+x'
    chmod u+x "$executable" || exit 1;

    echo "final bundled libs:" >&2;
    ls -l $dir/usr/* >&2
    just fetch https://github.com/linuxdeploy/linuxdeploy/releases/download/1-alpha-20250213-2/linuxdeploy-x86_64.AppImage appimage/linuxdeploy
    chmod u+x appimage/linuxdeploy

    export LD_LIBRARY_PATH="$dir/usr/lib:${LD_LIBRARY_PATH:-}"
    appimage/linuxdeploy \
    --appdir $dir \
    --desktop-file "$dir/com.frostsnapp.app.desktop" \
    --icon-file "$dir/Frostsnap.png" \
    --executable "$executable" \
    --output appimage

build-dmg: (build "macos" "--release")
    #!/bin/sh
    set -e

    # Configuration
    APP_NAME="Frostsnap"
    APP_PATH="build/macos/Build/Products/Release/${APP_NAME}.app"
    VERSION="${GITHUB_SHA:-$(git rev-parse --short HEAD 2>/dev/null || echo 'local')}"
    DMG_NAME="${APP_NAME}-${VERSION}-universal.dmg"

    # Check if app exists
    if [ ! -d "$APP_PATH" ]; then
        echo "ERROR: macOS build not found at $APP_PATH" >&2
        exit 1
    fi

    echo "Creating DMG installer: $DMG_NAME" >&2

    # Clean up any existing DMG
    rm -f "$DMG_NAME"

    # Check if create-dmg is available
    if ! command -v create-dmg &> /dev/null; then
        echo "ERROR: create-dmg is required but not installed" >&2
        echo "Install with: brew install create-dmg" >&2
        exit 1
    fi

    # Create professional DMG with background
    create-dmg \
      --volname "${APP_NAME}" \
      --volicon "macos/Runner/Assets.xcassets/AppIcon.appiconset/app_icon_1024.png" \
      --background "macos/dmg-background.png" \
      --window-pos 200 120 \
      --window-size 600 400 \
      --icon-size 100 \
      --icon "${APP_NAME}.app" 150 185 \
      --hide-extension "${APP_NAME}.app" \
      --app-drop-link 450 185 \
      --no-internet-enable \
      --hdiutil-quiet \
      "$DMG_NAME" \
      "$APP_PATH"

    echo "âœ… Created installer DMG: $DMG_NAME" >&2
    ls -lh "$DMG_NAME" >&2

clean:
    flutter clean
    cd rust && cargo clean
